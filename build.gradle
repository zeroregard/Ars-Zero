plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0+'
    id 'checkstyle'
}

base {
    version = "${minecraft_version}-${mod_version}"
    group = 'com.github.ars_zero'
    archivesBaseName = 'ars_zero'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
java.withSourcesJar()

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_version
        minecraftVersion = "1.21.1"
    }

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', "${project.mod_id},minecraft"
            def resolvedFilter = System.getProperty('filter', System.getProperty('ars_zero.testFilter', ''))
            systemProperty 'filter', resolvedFilter
            systemProperty 'ars_zero.testFilter', resolvedFilter
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', "${project.mod_id},minecraft"
            def resolvedFilter = System.getProperty('filter', System.getProperty('ars_zero.testFilter', ''))
            systemProperty 'filter', resolvedFilter
            systemProperty 'ars_zero.testFilter', resolvedFilter
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', "${project.mod_id},minecraft"
            def resolvedFilter = System.getProperty('filter', System.getProperty('ars_zero.testFilter', ''))
            systemProperty 'filter', resolvedFilter
            systemProperty 'ars_zero.testFilter', resolvedFilter
            logLevel = org.slf4j.event.Level.DEBUG
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        botClient {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', "${project.mod_id},minecraft"
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            gameDirectory = file('run-bot')
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
        "ars_zero_tests" {
            sourceSet sourceSets.test
        }
    }
}

sourceSets.main.resources { srcDir "src/generated/resources" }
sourceSets {
    test {
        resources {
            srcDirs = ['src/test/resources', 'src/main/resources']
        }
        compileClasspath += sourceSets.main.output + configurations.compileClasspath
        runtimeClasspath += sourceSets.main.output + configurations.runtimeClasspath
    }
}

repositories {
    mavenCentral()
    maven { url = "https://maven.neoforged.net/releases" }
    maven { url = "https://maven.saps.dev/releases" }
    maven { url = "https://maven.saps.dev/snapshots" }
    mavenLocal()
    maven { url = "https://modmaven.dev" }
    maven { url = "https://maven.minecraftforge.net" }
    maven { url = "https://maven.blamejared.com/" }
    maven { url = "https://maven.theillusivec4.top/" }
    maven { url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/" }
    maven { url = "https://jitpack.io" }
    maven { url = "https://maven.octo-studios.com/releases" }
    maven { 
        name = "RedlanceMinecraft"
        url = "https://repo.redlance.org/public"
    }
    exclusiveContent {
        forRepository {
            maven {
                url "https://cursemaven.com"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    implementation("com.hollingsworth.ars_nouveau:ars_nouveau-${minecraft_version}:${ars_version}")
    implementation('com.github.glitchfiend:TerraBlender-neoforge:1.21.1-4.1.0.8')
    
    implementation("com.alexthw.ars_elemental:ars_elemental-${minecraft_version}:${elemental_version}") {
        transitive = false
    }
    
    implementation("com.alexthw.sauce:sauce-${minecraft_version}:${sauce_version}")
    
    jarJar(implementation("com.alexthw.sauce:sauce-${minecraft_version}") {
        version {
            strictly '[0.0.2,)'
            prefer sauce_version
        }
    }) { transitive = false }

    implementation("software.bernie.geckolib:geckolib-neoforge-${minecraft_version}:${geckolib_version}")
    runtimeOnly("vazkii.patchouli:Patchouli:1.21-${patchouli_version}")
    implementation("top.theillusivec4.curios:curios-neoforge:${curios_version}+${minecraft_version}")

    compileOnly("mezz.jei:jei-1.21.1-neoforge-api:19.21.0.247")
    compileOnly("mezz.jei:jei-1.21.1-neoforge:19.21.0.247")
    runtimeOnly("mezz.jei:jei-1.21.1-neoforge:19.21.0.247")
    
    compileOnly "com.hollingsworth.nuggets:nuggets-neoforge-1.21.1:${nuggets_version}"

    testImplementation("net.neoforged:neoforge:${neo_version}")
    
}

tasks.withType(Copy).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        register('maven', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

checkstyle {
    toolVersion = '10.12.5'
    configFile = file('config/checkstyle/checkstyle.xml')
}



tasks.named("runGameTestServer").configure {
    def filterProperty = System.getProperty("ars_zero.testFilter")
    if (filterProperty != null && !filterProperty.isBlank()) {
        systemProperty "ars_zero.testFilter", filterProperty
    }
    doFirst {
        def sourceDir = file("gameteststructures")
        if (sourceDir.exists()) {
            copy {
                from(sourceDir)
                into(file("run/gameteststructures"))
            }
        }
        def mainResourceStructuresDir = file("src/main/resources/data/ars_zero/structure")
        if (mainResourceStructuresDir.exists()) {
            copy {
                from(mainResourceStructuresDir)
                into(file("run/gameteststructures/ars_zero"))
            }
        }
        def testResourceStructuresDir = file("src/test/resources/data/ars_zero/structure")
        if (testResourceStructuresDir.exists()) {
            copy {
                from(testResourceStructuresDir)
                into(file("run/gameteststructures/ars_zero"))
            }
        }
        def buildMainResourceStructuresDir = file("build/resources/main/data/ars_zero/structure")
        if (buildMainResourceStructuresDir.exists()) {
            copy {
                from(buildMainResourceStructuresDir)
                into(file("run/gameteststructures/ars_zero"))
            }
        }
        def buildTestResourceStructuresDir = file("build/resources/test/data/ars_zero/structure")
        if (buildTestResourceStructuresDir.exists()) {
            copy {
                from(buildTestResourceStructuresDir)
                into(file("run/gameteststructures/ars_zero"))
            }
        }
    }
}

